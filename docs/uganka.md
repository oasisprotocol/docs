# Oasisova uganka

Slovenska ekipa razvijalcev pri [Oasis Network][oasis-network] je za mlade
slovenske upe pripravila programersko uganko. Po캜iva na *tajni* blokovni
verigi z imenom [Oasis Sapphire][oasis-explorer]. Tvoja naloga je, da
napi코e코 kratko skripto, ki se pove쬰 s pametno pogodbo na na코em omre쬵u,
tajno pridobi vpra코anje in nanj odgovori. 캛e bo odgovor pravilen, osvoji코
kovan캜ke ROSE! 游꺛

## Blockchain, Ethereum, Solidity, Dapp, kaj za vraga?

Pozna코 le C? Python? Javascript? Odli캜no! Vsega ostalega se bo코 priu캜il. 游꿉

Porazdeljena omre쬵a niso enostavna re캜. Programiranje porazdeljenih
aplikacij ima veliko omejitev v primerjavi z namiznimi ali spletnimi,
zato je potrebno imeti odprto glavo. Poznavanje algoritmov
in podatkovnih struktur, pisanje programske kode, ki u캜inkovito re코i problem,
iznajdljivost pri iskanju hro코캜ev, natan캜no sledenje poteku programa z
vidika varnosti in kriptografske re코itve so cenjene vrline. 游뱁

## OK. Kaj moram storiti?

1. Najprej se nam pridru쬴 na kanalu `#slovenia` na
   [Oasisovem Discord stre쬹iku][oasis-discord]. Dostop do kanala pridobi코
   tako, da klikne코 na slovensko zastavico 游젏릖 v kanalu za pridobivanje vlog
   `#get-your-role`. 캛e 쬰li코, se nam lahko nato na kratko predstavi코, lahko pa
   napi코e코 zgolj `쬴vjo` in bomo zadovoljni. Z zanimanjem spremljamo slovenski
   kanal in ob nepremostljivi tehni캜ni oviri ti bomo z veseljem pomagali. Na
   neprimerna vpra코anja ti bomo odgovorili s 코e bolj neprimernim odgovorom. 游땔

2. 캛e se prvi캜 sre캜uje코 z razvojem aplikacij za blokovne verige, si oglej
   [kratek Ethereumov vodi캜 po pametnih pogodbah][ethereum-tutorial]. 游눠

3. Bravo! Po osvojenih osnovah Ethereumovih pametnih pogodb je zdaj na
   vrsti [snovanje *tajnih* pametnih pogodb na na코em dragocenem kamnu,
   Oasis Sapphire][oasis-sapphire-quickstart]. 游눑

4. Pametna pogodba uganke po캜iva na verigi
   [Oasis Sapphire Testnet][oasis-explorer-testnet], in sicer na naslovu
   `0xTODO`. `Riddle.sol` izgleda takole:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.9;

contract Riddle {
    string constant errInvalidCoupon = "Invalid coupon";
    string constant errWrongAnswer = "Wrong answer";

    // List of questions.
    string[] _questions;
    // List of answers.
    string[] _answers;
    // Coupon -> Private key mapping.
    mapping(string => string) _coupons;

    // Statistics.
    uint _numLookups;
    uint _numAnswers;
    uint _numCorrectAnswers;

    constructor(string[] memory questions, string[] memory answers, string[] memory couponCodes, string[] memory couponKeys) payable {
        require(questions.length == answers.length, "List of questions must match list of answers.");
        require(couponCodes.length == couponKeys.length, "Coupon codes must match coupon keys.");

        _questions = questions;
        _answers = answers;
        for (uint i=0; i<couponCodes.length; i++) {
            _coupons[couponCodes[i]] = couponKeys[i];
        }
    }

    // Return question for given coupon.
    function getQuestion(string memory coupon) external returns (string memory) {
        require(bytes(_coupons[coupon]).length != 0, errInvalidCoupon);
        _numLookups++;

        return _questions[computeQuestionIndex(coupon)];
    }

    // Check the answer to the question for given coupon.
    function submitAnswer(string memory coupon, string memory answer) external returns (string memory) {
        require(bytes(_coupons[coupon]).length != 0, errInvalidCoupon);
        _numAnswers++;
        require(keccak256(bytes(_answers[computeQuestionIndex(coupon)])) == keccak256(bytes(answer)), errWrongAnswer);
        _numCorrectAnswers++;

        return string.concat("Congratulations, your answer is correct! Use this private key to access reward on Oasis Sapphire Mainnet: ", _coupons[coupon]);
    }

    // Compute the index of the question for given coupon.
    function computeQuestionIndex(string memory coupon) private view returns (uint) {
        return uint(keccak256(bytes(coupon))) % _questions.length;
    }

    // Return statistics.
    // NB: This can include duplicate queries and answers!
    function getStatistics() external view returns (uint, uint, uint) {
        return (_numLookups, _numAnswers, _numCorrectAnswers);
    }
}
```

5. Tvoja naloga je, da s pomo캜jo osvojenih orodij napi코e코 skripto, ki
   komunicira s pametno pogodbo uganke. Najprej s *tajno transakcijo* do
   `getQuestion()` poizvedi za vpra코anjem, vezanem na tvoj
   kupon z letaka. Nato sestavi odgovor in ga s tajno transakcijo do
   `submitAnswer()` po코lji nazaj. 캛e bo odgovor pravilen, se ti razkrije
   zasebni klju캜 do 캜isto pravcatih kovan캜kov ROSE na verigi
   [Oasis Sapphire Mainnet][oasis-explorer]. 游꿀

6. 캛estitamo! Kovan캜ke lahko uporabi코 za objavo novih porazdeljenih
   aplikacij na omre쬵u Oasis in med prvimi na svetu izkoristi코 *tajnost*
   pametnih pogodb za uresni캜itev svojih idej. Lahko pa kovan캜ke pretopi코 v
   ~~pivo~~ sok zate in za tvoje prijatelje v
   [bli쬹jem kripto lokalu][bitcoin-map]. 游꽅

[oasis-network]: https://oasisprotocol.org
[oasis-discord]: https://discord.gg/oasisprotocol
[ethereum-tutorial]: https://ethereum.org/en/developers/tutorials/hello-world-smart-contract-fullstack/
[oasis-sapphire-quickstart]: https://docs.oasis.io/dapp/sapphire/quickstart
[oasis-explorer-testnet]: https://testnet.explorer.sapphire.oasis.dev/
[oasis-explorer]: https://explorer.sapphire.oasis.io/
[bitcoin-map]: https://map.bitcoin.com
